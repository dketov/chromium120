# Copyright 2019 LG Electronics, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

import("//build/config/locales.gni")
import("//neva/neva.gni")
import("//webos/install/cbe.gni")
import("//webos/install/install.gni")
import("//webos/install/paths.gni")
import("//webos/install/sysbus.gni")

install_manifest("browser_manifest") {
  sources = [ "com.webos.app.browser.manifest.json" ]
}

install_legacy_roles("browser_legacy_roles") {
  app = "$browser_app"
  exename = "$browser_exe"
  sources = [ "legacy/com.webos.app.browser.json" ]
}

install_legacy_services("browser_legacy_services") {
  app = "$browser_app"
  exename = "$browser_exe"
}

install_containers("browser_container") {
  sources = [ "//webos/install/services/app.container.json.in" ]
  exename = "$browser_exe"
  app = "$browser_app"
}

install_services("browser_services") {
  app = "$browser_app"
  exename = "$browser_exe"
}

install_contemporary_roles("browser_roles") {
  app = "$browser_app"
  exename = "$browser_exe"
  sources = [ "//webos/install/services/role.json.in" ]
}

install_client_permissions("browser_permissions") {
  sources = [ "com.webos.app.browser.perm.json" ]
}

install_scripts("browser_scripts") {
  sources = [ "run_webbrowser",
              "kill_webbrowser",
              "mem_webbrowser" ]
  outputs = [ "$image_path/$browser_dir/" ]
}

copy("default_browser") {
  sources = [ "$root_out_dir/chrome",
              "$root_out_dir/chrome_100_percent.pak",
              "$root_out_dir/chrome_200_percent.pak",
              "$root_out_dir/headless_lib_data.pak",
              "$root_out_dir/headless_lib_strings.pak",
              "$root_out_dir/resources.pak",
              "$root_out_dir/icudtl.dat",
              "$root_out_dir/snapshot_blob.bin",
              "$root_out_dir/product_logo_48.png",
              "appinfo.json",
              "webbrowser_icon.png",
              "webbrowser_splash.png" ]

  outputs = [ "$image_path/$browser_dir/{{source_file_part}}" ]

  deps = [
    "//chrome",                             # for chrome, chrome_*.pack and resources.pak
    "//v8:v8",                              # for snapshot_blob.bin and natives_blob.bin
    "//third_party/icu:icudata",            # for icudtl.dat
    "//headless:resource_pack_data",        # for headless_lib_data.pak
    "//headless:resource_pack_strings",     # for headless_lib_strings.pak
    ":browser_scripts",
    ":locales",
  ]

  if (can_use_legacy_luna_role_files) {
    deps += [
      ":browser_legacy_roles",
      ":browser_legacy_services",
    ]
  } else {
    deps += [
      ":browser_permissions",
      ":browser_roles",
      ":browser_services",
    ]
  }
}

copy("locales") {
  sources = []
  foreach(locale, platform_pak_locales) {
    sources += [ "$root_out_dir/locales/$locale.pak" ]
  }

  outputs = [ "$image_path/$browser_dir/locales/{{source_file_part}}" ]
  deps = [ "//chrome:packed_resources" ]
}
