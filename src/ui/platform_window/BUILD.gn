# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//neva/neva.gni")

if (use_neva_media) {
  mojom("mojom") {
    sources = [ "neva/mojom/video_window.mojom" ]
    public_deps = [
      "//mojo/public/mojom/base",
      "//sandbox/policy/mojom",
      "//ui/gfx/geometry/mojom",
      "//ui/gfx/mojom",
    ]

    cpp_typemaps = [
      {
        types = [
          {
            mojom = "ui.mojom.VideoWindowInfo"
            cpp = "::ui::VideoWindowInfo"
          },
          {
            mojom = "ui.mojom.VideoWindowParams"
            cpp = "::ui::VideoWindowParams"
          },
        ]
        traits_headers = [
          "neva/video_window_info.h",
          "neva/mojom/video_window_mojom_traits.h",
        ]
        traits_sources = [ "neva/mojom/video_window_mojom_traits.cc" ]
        traits_public_deps = [ "//base" ]
      },
    ]
  }
}

component("platform_window") {
  sources = [
    "extensions/workspace_extension.cc",
    "extensions/workspace_extension.h",
    "extensions/workspace_extension_delegate.h",
    "platform_window.cc",
    "platform_window.h",
    "platform_window_delegate.cc",
    "platform_window_delegate.h",
    "platform_window_init_properties.cc",
    "platform_window_init_properties.h",
  ]

  defines = [ "IS_PLATFORM_WINDOW_IMPL" ]

  public_deps = []

  deps = [
    "//base",
    "//ui/base",
    "//ui/gfx",
  ]

  #@name USE_NEVA_APPRUNTIME
  #@{
  sources += [
    "neva/platform_window.h",
    "neva/platform_window_delegate.h",
    "neva/ui_utils.cc",
    "neva/ui_utils.h",
    "neva/window_group_configuration.cc",
    "neva/window_group_configuration.h",
    "neva/window_group_configuration_param_traits.cc",
    "neva/window_group_configuration_param_traits.h",
    "neva/window_group_configuration_param_traits_macros.h",
    "neva/window_group_ipc_export.h",
    "neva/xinput_types.h",
  ]

  deps += [ "//ipc" ]

  #@}

  if (is_webos) {
    sources += [ "neva/webos/platform_window_webos.h" ]
  }

  if (use_neva_media) {
    sources += [
      "neva/video_window_geometry_manager.h",
      "neva/video_window_info.h",
    ]
    deps += [ ":mojom" ]
  }

  if (is_fuchsia) {
    sources += [
      "fuchsia/initialize_presenter_api_view.cc",
      "fuchsia/initialize_presenter_api_view.h",
      "fuchsia/scenic_window_delegate.h",
      "fuchsia/view_ref_pair.cc",
      "fuchsia/view_ref_pair.h",
    ]

    public_deps += [
      "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.element:fuchsia.element_hlcpp",
      "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.ui.views:fuchsia.ui.views_hlcpp",
      "//third_party/fuchsia-sdk/sdk/pkg/scenic_cpp",
      "//third_party/fuchsia-sdk/sdk/pkg/sys_cpp",
    ]
  }

  if (is_linux || is_chromeos) {
    sources += [
      "extensions/desk_extension.cc",
      "extensions/desk_extension.h",
      "extensions/pinned_mode_extension.cc",
      "extensions/pinned_mode_extension.h",
      "extensions/system_modal_extension.cc",
      "extensions/system_modal_extension.h",
      "extensions/wayland_extension.cc",
      "extensions/wayland_extension.h",
    ]
    deps += [ "//ui/base/dragdrop/mojom:mojom_headers" ]
  }
  if (is_linux || is_chromeos_ash) {
    sources += [
      "extensions/x11_extension.cc",
      "extensions/x11_extension.h",
      "extensions/x11_extension_delegate.h",
    ]
  }
}

group("platform_impls") {
  public_deps = [ "//ui/platform_window/stub" ]
  if (is_win) {
    public_deps += [ "//ui/platform_window/win" ]
  }
}
